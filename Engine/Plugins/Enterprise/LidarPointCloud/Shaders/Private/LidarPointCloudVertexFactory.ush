// Copyright Epic Games, Inc. All Rights Reserved.

#include "/Engine/Private/Common.ush"
#include "/Engine/Private/VertexFactoryCommon.ush"

/*=============================================================================
	LidarPointCloudVertexFactory.usf: LiDAR Point Cloud vertex factory shader code.
=============================================================================*/

// First half is normal, second half has inverted V
static float2 PrecomputedUV_Quad[8] = { float2(-0.5, -0.5), float2(0.5, -0.5), float2(0.5, 0.5), float2(-0.5, 0.5), float2(0, 1), float2(1, 1), float2(1, 0), float2(0, 0) };

// Used to detect if the instance is rendered inside the editor viewport
uint bEditorView;

// Selection
half3 SelectionColor;

// Alignment
float3 LocationOffset;

// Used for sprite size calculation
float VirtualDepth;
float SpriteSizeMultiplier;
float RootCellSize;
float3 RootExtent;
uint bUsePerPointScaling;
float ReversedVirtualDepthMultiplier;
Buffer<float> TreeBuffer;

// Needed for WPO calculations
float3 ViewRightVector;
float3 ViewUpVector;
uint bUseCameraFacing;
uint bUseScreenSizeScaling;
uint bUseStaticBuffers;

// Used for coloration override
float3 BoundsSize;
half3 ElevationColorBottom;
half3 ElevationColorTop;

// Color Adjustment
half4 Offset;
half4 Contrast;
half4 Saturation;
half4 Gamma;
half3 Tint;
float IntensityInfluence;

// Flags
uint bUseCircle;
uint bUseColorOverride;
uint bUseElevationColor;
uint bUseClassification;

float4 ClassificationColors[32];

Buffer<float> DataBuffer;

/**
 * [Tx, Ty, Tz, Invert]
 * [Fx, Fy, Fz, Ex]
 * [Rx, Ry, Rz, Ey]
 * [Ux, Uy, Uz, Ez]
 */
float4x4 ClippingVolume[16];
uint NumClippingVolumes;
uint bStartClipped;

#if USE_INSTANCING
	// Instanced stereo sets the eye index explicitly for instanced geometry
	#if INSTANCED_STEREO
		uint InstancedEyeIndex;
	#endif
#endif

struct FVertexFactoryInput
{
    float4	Position    : ATTRIBUTE0;
    half4	Color		: ATTRIBUTE1;
    uint	MetaData	: ATTRIBUTE2;
	uint    VertexId    : SV_VertexID;
};

struct FPositionOnlyVertexFactoryInput
{
	float4  Position    : ATTRIBUTE0;
	uint    VertexId    : SV_VertexID;
};

struct FPositionAndNormalOnlyVertexFactoryInput
{
	float4  Position	: ATTRIBUTE0;
	float4	Normal		: ATTRIBUTE2;
	uint    VertexId	: SV_VertexID;
};

struct FVertexFactoryInterpolantsVSToPS
{
	TANGENTTOWORLD_INTERPOLATOR_BLOCK

	half4	Color : COLOR0;
	float2	TexCoords : TEXCOORD0;

#if INSTANCED_STEREO
	nointerpolation uint EyeIndex : PACKED_EYE_INDEX;
#endif
};

struct FVertexFactoryIntermediates
{
    half3x3 TangentToLocal;
    half3x3 TangentToWorld;
    half TangentToWorldSign;
    half4 Color;
	float3 Position;
	half2 Scale;
	float SpriteSize;
};

struct FPointCloudInstanceData
{
	float3 Position;
	uint Color;
	uint bSelected;
	uint Classification;
	half3x3 TangentToLocal;
	float SpriteSize;
};

float3 ApplyClipping(float3 Position)
{
	bool bClip = bStartClipped;

	// Only process clipping if not in the editor view
	if (!bEditorView)
	{
		for (uint i = 0; i < NumClippingVolumes; i++)
		{
			float3 DeltaPosition = TransformLocalToWorld(Position).xyz - ClippingVolume[i][0].xyz;
			bool bInsideClippingBox = abs(dot(DeltaPosition, ClippingVolume[i][1].xyz)) <= ClippingVolume[i][1].w && abs(dot(DeltaPosition, ClippingVolume[i][2].xyz)) <= ClippingVolume[i][2].w && abs(dot(DeltaPosition, ClippingVolume[i][3].xyz)) <= ClippingVolume[i][3].w;

			if (ClippingVolume[i][0][3] == 1)
			{
				if (bInsideClippingBox)
				{
					bClip = true;
				}
			}
			else
			{
				if (bInsideClippingBox)
				{
					bClip = false;
				}
			}
		}
	}

	return (!bEditorView && bClip) ? 0.0f / 0 : Position;
}

float3 CalcPosition(float3 Position)
{
	return ApplyClipping(Position + LocationOffset);
}

half3x3 CalcTangentToLocal(uint MetaData)
{
	half3x3 TangentToLocal;

	if (bUseCameraFacing)
	{
		// In this mode, [0] and [1] are only really used for scaling
		TangentToLocal[0] = ViewRightVector;
		TangentToLocal[1] = ViewUpVector;

		// Hardcoded to have the lighting behave more correctly
		TangentToLocal[2] = float3(0, 0, 1);
	}
	else
	{
		half3 Normal = half3((0x000000FF & MetaData) / 127.5f - 1, (0x000000FF & (MetaData >> 8)) / 127.5f - 1, (0x000000FF & (MetaData >> 16)) / 127.5f - 1);

		// Force camera facing
		if (dot(Normal, Normal) < 0.1f)
		{
			// In this mode, [0] and [1] are only really used for scaling
			TangentToLocal[0] = ViewRightVector;
			TangentToLocal[1] = ViewUpVector;

			// Hardcoded to have the lighting behave more correctly
			TangentToLocal[2] = float3(0, 0, 1);
		}
		else
		{
			half3 N = abs(Normal);

			half3 Tangent = half3(1, 0, 0);

			// Find best basis vectors.
			if (N.z > N.x && N.z > N.y)
			{
				Tangent = half3(1, 0, 0);
			}
			else
			{
				Tangent = half3(0, 0, 1);
			}

			Tangent = (Tangent - Normal * dot(Tangent, Normal));
			Tangent = Tangent * rsqrt(dot(Tangent, Tangent));

			TangentToLocal[0] = Tangent;
			TangentToLocal[1] = cross(Tangent, Normal);
			TangentToLocal[2] = Normal;
		}
	}

	return TangentToLocal;
}

half3x3 CalcTangentToWorldNoScale(in half3x3 TangentToLocal)
{
	half3x3 LocalToWorld = GetLocalToWorld3x3();
	half3 InvScale = Primitive.InvNonUniformScaleAndDeterminantSign.xyz;
	LocalToWorld[0] *= InvScale.x;
	LocalToWorld[1] *= InvScale.y;
	LocalToWorld[2] *= InvScale.z;
	return mul(TangentToLocal, LocalToWorld);
}

float CalcSpriteSize(float3 Position)
{
	float VD = VirtualDepth;

	if (bUsePerPointScaling)
	{
		VD = 0;
		uint Idx = 0;
		float3 NodeExtent = RootExtent;
		float3 NodeCenter = LocationOffset;

		while (true)
		{
			uint TreeBufferData = asuint(TreeBuffer[Idx]);
			uint ChildrenBitmask = 0x000000FF & TreeBufferData;

			float3 CenterRelativeLocation = Position - NodeCenter;
			uint ChildNodeIndex = (CenterRelativeLocation.x > 0 ? 4 : 0) + (CenterRelativeLocation.y > 0 ? 2 : 0) + (CenterRelativeLocation.z > 0);

			if ((ChildrenBitmask & (1 << ChildNodeIndex)) == 0)
			{
				break;
			}

			Idx += (0x0000FFFF & (TreeBufferData >> 8));
			for (int i = 0; i < 8; i++)
			{
				if (i == ChildNodeIndex)
				{
					break;
				}

				if ((ChildrenBitmask & (1 << i)) != 0)
				{
					Idx++;
				}
			}

			VD = (0x000000FF & (TreeBufferData >> 24)) * ReversedVirtualDepthMultiplier;
			NodeCenter += NodeExtent * (float3((ChildNodeIndex & 4) == 4, (ChildNodeIndex & 2) == 2, (ChildNodeIndex & 1) == 1) - 0.5f);
			NodeExtent *= 0.5f;
		}
	}
	
	return RootCellSize / pow(2.0f, VD);
}

float3 ProcessPosition(float3 Position, half3x3 TangentToLocal, half2 Scale, float SpriteSize)
{
	float Size = bUseScreenSizeScaling ? mul(float4(Position.xyz, 1), ResolvedView.TranslatedWorldToView).z * 0.01f : SpriteSize;
	return Position + (TangentToLocal[0] * Scale.x + TangentToLocal[1] * Scale.y) * SpriteSizeMultiplier * Size;
}

float3 ProcessPosition(float3 Position, half2 Scale)
{
	half3x3 TangentToLocal;
	TangentToLocal[0] = ViewRightVector;
	TangentToLocal[1] = ViewUpVector;
	return ProcessPosition(Position, TangentToLocal, Scale, CalcSpriteSize(Position));
}

FPointCloudInstanceData GetInstanceData(uint VertexId)
{
	FPointCloudInstanceData InstanceData = (FPointCloudInstanceData)0;

	uint PointId = VertexId / 4;
	uint Idx = PointId * 5;
	InstanceData.Position = float3(DataBuffer[Idx], DataBuffer[Idx + 1], DataBuffer[Idx + 2]) + LocationOffset;
	InstanceData.Color = asuint(DataBuffer[Idx + 3]);

	/**
	 * 0		bVisible
	 * 1 - 5	Classification
	 * 6		bSelected
	 * 7		Unused
	 */
	uint MetaData = 0x000000FF & (asuint(DataBuffer[Idx + 4]) >> 24);

	InstanceData.Classification = 0x0000001F & (MetaData >> 1);
	InstanceData.bSelected = bEditorView && (0x00000040 & MetaData);
	InstanceData.TangentToLocal = CalcTangentToLocal(asuint(DataBuffer[Idx + 4]));
	InstanceData.Position = ApplyClipping(InstanceData.Position);
	InstanceData.SpriteSize = CalcSpriteSize(InstanceData.Position);

	return InstanceData;
}

FVertexFactoryIntermediates GetVertexFactoryIntermediates(FVertexFactoryInput Input)
{
	FVertexFactoryIntermediates Intermediates = (FVertexFactoryIntermediates)0;
	
	uint bSelected;
	uint Classification;
	half3 Color;
	half Intensity;
	float SpriteSize;

	if (bUseStaticBuffers)
	{
		Intermediates.Position = CalcPosition(Input.Position.xyz);
		Intermediates.TangentToLocal = CalcTangentToLocal(Input.MetaData);

		uint MetaData = 0x000000FF & (Input.MetaData >> 24);

		bSelected = bEditorView && (0x00000040 & MetaData);
		Classification = 0x0000001F & (MetaData >> 1);

		SpriteSize = CalcSpriteSize(Intermediates.Position);

		Color = Input.Color.rgb;
		Intensity = Input.Color.a;
	}
	else
	{
		FPointCloudInstanceData InstanceData = GetInstanceData(Input.VertexId);
		Intermediates.Position = InstanceData.Position;
		Intermediates.TangentToLocal = InstanceData.TangentToLocal;
		bSelected = InstanceData.bSelected;
		Classification = InstanceData.Classification;
		SpriteSize = InstanceData.SpriteSize;
		Color = half3(0x000000FF & (InstanceData.Color >> 16), 0x000000FF & (InstanceData.Color >> 8), 0x000000FF & InstanceData.Color) * 0.003921568627451;
		Intensity = (0x000000FF & (InstanceData.Color >> 24)) * 0.003921568627451;
	}

	Intermediates.SpriteSize = SpriteSize;
	Intermediates.Scale = PrecomputedUV_Quad[Input.VertexId % 4];
	Intermediates.TangentToWorldSign = Primitive.InvNonUniformScaleAndDeterminantSign.w;
	Intermediates.TangentToWorld = CalcTangentToWorldNoScale(Intermediates.TangentToLocal);
	
	// Color
	{
		// Used to account for offset due to shifted points bounds
		float3 RelativePosition = Intermediates.Position - LocationOffset;

		half3 ElevationColor = lerp(ElevationColorBottom, ElevationColorTop, clamp((RelativePosition.z + BoundsSize.z * 0.5) / BoundsSize.z, 0, 1));
		half3 PositionColor = (RelativePosition + BoundsSize * 0.5) / BoundsSize;
		half3 ClassificationColor = ClassificationColors[0x0000001F & (Classification >> 1)].rgb;

		// Pick color source
		Color = lerp(lerp(Color, lerp(lerp(PositionColor, ClassificationColor, bUseClassification), ElevationColor, bUseElevationColor), bUseColorOverride), SelectionColor, bSelected);

		// Mix with Intensity
		Color = lerp(Color, Color * Intensity, IntensityInfluence);

		// Color Adjustment
		{
			Color = lerp(dot(Color, half3(0.299, 0.587, 0.144)), Color, Saturation.rgb * Saturation.a);
			Color *= Tint;
			Color = pow(Color, 2.2 * Gamma.rgb * Gamma.a);
			Color = (Color - 0.5) * Contrast.rgb * Contrast.a + 0.5;
			Color += Offset.rgb * Offset.a;
			Color = clamp(Color, 0, 1);
		}

		Intermediates.Color = half4(Color, bUseCircle);
	}

	return Intermediates;
}

FVertexFactoryInterpolantsVSToPS VertexFactoryGetInterpolantsVSToPS(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
{
	FVertexFactoryInterpolantsVSToPS Interpolants = (FVertexFactoryInterpolantsVSToPS)0;
	Interpolants.TangentToWorld0 = float4(Intermediates.TangentToWorld[0], 0);
	Interpolants.TangentToWorld2 = float4(Intermediates.TangentToWorld[2], Intermediates.TangentToWorldSign);
	Interpolants.Color = VertexParameters.VertexColor;
	Interpolants.TexCoords = PrecomputedUV_Quad[Input.VertexId % 4 + 4];    // Using inverted V

#if INSTANCED_STEREO
	Interpolants.EyeIndex = 0;
#endif

	return Interpolants;
}

half3x3 VertexFactoryGetTangentToLocal(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates) { return Intermediates.TangentToLocal; }

// @return translated world position
float4 VertexFactoryGetWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	float4 WorldPosition = TransformLocalToTranslatedWorld(Intermediates.Position);
	WorldPosition.xyz = ProcessPosition(WorldPosition.xyz, Intermediates.TangentToLocal, Intermediates.Scale, Intermediates.SpriteSize);
	return WorldPosition;
}
float4 VertexFactoryGetWorldPosition(FPositionOnlyVertexFactoryInput Input)
{
	if (bUseStaticBuffers)
	{
		float4 WorldPosition = TransformLocalToTranslatedWorld(CalcPosition(Input.Position.xyz));
		WorldPosition.xyz = ProcessPosition(WorldPosition.xyz, PrecomputedUV_Quad[Input.VertexId % 4]);
		return WorldPosition;
	}
	else
	{
		FPointCloudInstanceData InstanceData = GetInstanceData(Input.VertexId);
		float4 WorldPosition = TransformLocalToTranslatedWorld(InstanceData.Position);
		WorldPosition.xyz = ProcessPosition(WorldPosition.xyz, InstanceData.TangentToLocal, PrecomputedUV_Quad[Input.VertexId % 4], InstanceData.SpriteSize);
		return WorldPosition;
	}
}
float4 VertexFactoryGetRasterizedWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float4 InWorldPosition) { return InWorldPosition; }
float3 VertexFactoryGetPositionForVertexLighting(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 TranslatedWorldPosition) { return TranslatedWorldPosition; }
float3 VertexFactoryGetWorldNormal(FPositionAndNormalOnlyVertexFactoryInput Input)
{
	return RotateLocalToWorld(bUseStaticBuffers ? Input.Normal.xyz : GetInstanceData(Input.VertexId).TangentToLocal[2], 0);
}
float4 VertexFactoryGetWorldPosition(FPositionAndNormalOnlyVertexFactoryInput Input)
{
	if (bUseStaticBuffers)
	{
		float4 WorldPosition = TransformLocalToTranslatedWorld(CalcPosition(Input.Position.xyz), 0);
		WorldPosition.xyz = ProcessPosition(WorldPosition.xyz, PrecomputedUV_Quad[Input.VertexId % 4]);
		return WorldPosition;
	}
	else
	{
		FPointCloudInstanceData InstanceData = GetInstanceData(Input.VertexId);
		float4 WorldPosition = TransformLocalToTranslatedWorld(InstanceData.Position, 0);
		WorldPosition.xyz = ProcessPosition(WorldPosition.xyz, InstanceData.TangentToLocal, PrecomputedUV_Quad[Input.VertexId % 4], InstanceData.SpriteSize);
		return WorldPosition;
	}
}
float3 VertexFactoryGetWorldNormal(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates) { return Intermediates.TangentToWorld[2]; }

// @return previous translated world position
float4 VertexFactoryGetPreviousWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	float4x4 PreviousLocalToWorldTranslated = GetPrimitiveData(0).PreviousLocalToWorld;
    PreviousLocalToWorldTranslated[3][0] += ResolvedView.PrevPreViewTranslation.x;
    PreviousLocalToWorldTranslated[3][1] += ResolvedView.PrevPreViewTranslation.y;
    PreviousLocalToWorldTranslated[3][2] += ResolvedView.PrevPreViewTranslation.z;
    return mul(float4(Intermediates.Position, 1), PreviousLocalToWorldTranslated);
}

/** Converts from vertex factory specific input to a FMaterialVertexParameters, which is used by vertex shader material inputs. */
FMaterialVertexParameters GetMaterialVertexParameters(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 WorldPosition, half3x3 TangentToLocal)
{
    FMaterialVertexParameters Result = (FMaterialVertexParameters) 0;
    
	Result.WorldPosition = WorldPosition;
    Result.TangentToWorld = Intermediates.TangentToWorld;
    Result.PreSkinnedNormal = TangentToLocal[2];
    Result.PreSkinnedPosition = WorldPosition;
    Result.VertexColor = Intermediates.Color;

#if NUM_MATERIAL_TEXCOORDS_VERTEX
	Result.TexCoords[0] = PrecomputedUV_Quad[Input.VertexId % 4];
	#if NUM_MATERIAL_TEXCOORDS_VERTEX > 1
		Result.TexCoords[1] = Intermediates.Scale;
	#endif
#endif

    return Result;
}

FMaterialPixelParameters GetMaterialPixelParameters(FVertexFactoryInterpolantsVSToPS Interpolants, float4 SvPosition)
{
    FMaterialPixelParameters Result = MakeInitializedMaterialPixelParameters();

    Result.Particle.Color = half4(1, 1, 1, 1);
    Result.TwoSidedSign = 1;
	Result.VertexColor = Interpolants.Color;
	Result.VertexColor.a = lerp(1, 1 - distance(Interpolants.TexCoords, 0.5), Interpolants.Color.a);

    half3 TangentToWorld0 = Interpolants.TangentToWorld0.xyz;
    half4 TangentToWorld2 = Interpolants.TangentToWorld2;
    Result.UnMirrored = TangentToWorld2.w;
    Result.TangentToWorld = AssembleTangentToWorld(TangentToWorld0, TangentToWorld2);

#if NUM_TEX_COORD_INTERPOLATORS
    Result.TexCoords[0] = Interpolants.TexCoords;
#endif

    return Result;
}

#if USE_INSTANCING
float4 VertexFactoryGetInstanceHitProxyId(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates) { return 0; }
#endif

float4 VertexFactoryGetTranslatedPrimitiveVolumeBounds(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return float4(Primitive.ObjectWorldPositionAndRadius.xyz + ResolvedView.PreViewTranslation.xyz, Primitive.ObjectWorldPositionAndRadius.w);
}

#if INSTANCED_STEREO
uint VertexFactoryGetEyeIndex(uint InstanceId)
{
	#if USE_INSTANCING
		return InstancedEyeIndex;
	#else
		return InstanceId & 1;
	#endif
}
#endif

#if USING_TESSELLATION
	struct FVertexFactoryInterpolantsVSToDS
	{
		FVertexFactoryInterpolantsVSToPS InterpolantsVSToPS;
	};

	float2 VertexFactoryGetTextureCoordinateDS( FVertexFactoryInterpolantsVSToDS Interpolants )
	{
#if NUM_MATERIAL_TEXCOORDS
		return Interpolants.InterpolantsVSToPS.TexCoords.xy;
#else // #if NUM_MATERIAL_TEXCOORDS
		return float2(0,0);
#endif // #if NUM_MATERIAL_TEXCOORDS
	}

	FVertexFactoryInterpolantsVSToPS VertexFactoryAssignInterpolants(FVertexFactoryInterpolantsVSToDS Input)
	{
		return Input.InterpolantsVSToPS;
	}

	/** Converts from vertex factory specific interpolants to a FMaterialTessellationParameters, which is used by material inputs. */
	FMaterialTessellationParameters GetMaterialTessellationParameters(FVertexFactoryInterpolantsVSToDS Interpolants, float3 CameraLocalWorldPosition)
	{
		FMaterialTessellationParameters	Result;

#if NUM_TEX_COORD_INTERPOLATORS
		Result.TexCoords[0] = Interpolants.InterpolantsVSToPS.TexCoords;
#endif // NUM_TEX_COORD_INTERPOLATORS

#if INTERPOLATE_VERTEX_COLOR
		Result.VertexColor = Interpolants.InterpolantsVSToPS.Color;
#endif	// INTERPOLATE_VERTEX_COLOR

		half3 TangentToWorld0 = Interpolants.InterpolantsVSToPS.TangentToWorld0.xyz;
		half4 TangentToWorld2 = Interpolants.InterpolantsVSToPS.TangentToWorld2;

		Result.TangentToWorld = AssembleTangentToWorld( TangentToWorld0, TangentToWorld2 );

		Result.TangentToWorldPreScale = 1;
		Result.WorldPosition = CameraLocalWorldPosition + ResolvedView.WorldCameraOrigin;

		return Result;
	}

	FVertexFactoryInterpolantsVSToDS VertexFactoryGetInterpolantsVSToDS(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
	{
		FVertexFactoryInterpolantsVSToDS Interpolants;
		Interpolants.InterpolantsVSToPS = VertexFactoryGetInterpolantsVSToPS(Input, Intermediates, VertexParameters);
		return Interpolants;
	}

	FVertexFactoryInterpolantsVSToDS VertexFactoryInterpolate(FVertexFactoryInterpolantsVSToDS a, float aInterp, FVertexFactoryInterpolantsVSToDS b, float bInterp)
	{
		FVertexFactoryInterpolantsVSToDS O;
	
		// Do we really need to interpolate TangentToWorld2 here? It should be replaced by the
		// interpolated normal from 'whatever' interpolation scheme we're using
	
		TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.TangentToWorld0.xyz);
		TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.TangentToWorld2);

#if INTERPOLATE_VERTEX_COLOR
		TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.Color);
#endif

#if NUM_TEX_COORD_INTERPOLATORS
		TESSELLATION_INTERPOLATE_MEMBER(InterpolantsVSToPS.TexCoords);
#endif

		return O;
	}

	float3x3 VertexFactoryGetTangentToLocalDS(FVertexFactoryInterpolantsVSToDS Interpolants)
	{
		// This duplicates stuff already going on in GetMaterialTessellationParameters(), so
		// maybe the hull shader could leverage that instead?
		half3 TangentToWorld0 = Interpolants.InterpolantsVSToPS.TangentToWorld0.xyz;
		half4 TangentToWorld2 = Interpolants.InterpolantsVSToPS.TangentToWorld2;
		float3x3 TangentToWorld = AssembleTangentToWorld( TangentToWorld0, TangentToWorld2 );

		return TangentToWorld;
	}
#endif // #if USING_TESSELLATION

uint VertexFactoryGetPrimitiveId(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}